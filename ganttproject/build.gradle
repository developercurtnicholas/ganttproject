apply plugin: 'java'
apply plugin: 'idea'

ext {
  libDir = 'lib/core'
}

dependencies {
  compile fileTree(dir: project.ext.libDir, include: ['*.jar'])
  compile project(':..:biz.ganttproject.core')
  mavenDeps group: 'org.controlsfx', name: 'controlsfx', version: '8.40.10'
}

updateMavenDeps.doFirst {
  into project.ext.libDir
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
        }
    }
}

task copyPluginFiles(type: Copy) {
    into(new File(rootProject.pluginsDir, project.name))
    from(jar.outputs.getFiles().getFiles().flatten())
    from(fileTree(".")) {
      include "plugin.xml"
      include "lib/core/**.jar"
      exclude "lib/core/eclipsito.jar"
      include "data/resources/**.properties"
      include "data/resources/calendar/**"
      include "data/resources/icons/**"
      include "data/resources/language/**"
    }
}

task copyEclipsito(type: Copy) {
    into(rootProject.distBinDir)
    from(fileTree("lib/core")) {
      include "eclipsito.jar"
    }
    outputs.upToDateWhen {
      new File(rootProject.distBinDir, "eclipsito.jar").exists()
    }
}

task copyPlugin(dependsOn: ['copyPluginFiles', 'copyEclipsito']) << { println "Copying $project.name to $rootProject.pluginsDir" }
